buildscript {
    ext {
        kotlin_version = '1.0.0'
        anko_version = '0.8.3'
        kotson_version = '2.1.0'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.6"
}

allprojects {
    group = 'org.rerenderer'
    version = '0.3.7-SNAPSHOT'
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven'
apply plugin: 'maven-publish'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.anko:anko-sdk15:$anko_version"
    compile "com.github.salomonbrys.kotson:kotson:$kotson_version"

    androidTestCompile 'junit:junit:4.12'
    androidTestCompile "org.mockito:mockito-core:1.9.5"
    androidTestCompile 'com.android.support:support-annotations:23.2.0'
    androidTestCompile 'com.android.support.test:runner:0.4.1'
    androidTestCompile 'com.android.support.test:rules:0.4.1'
    androidTestCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    androidTestCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    repositories {
        mavenCentral()
        jcenter()
    }
}

android {
    compileSdkVersion 15
    buildToolsVersion '22.0.1'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 15
        versionCode 1
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        androidTests.assets.srcDirs += 'src/androidTest/assets'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    publications = ['MyPublication']
    pkg {
        repo = 'rerenderer'
        name = 'android'
        userOrg = 'rerenderer'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/rerenderer/rerenderer-android.git'

        version {
            name = project.version
            released = new Date()
        }
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            artifactId 'android'
            artifact(sourceJar)
            artifact "${project.buildDir}/outputs/aar/${project.name}-release.aar"

            // From https://stackoverflow.com/questions/24743562/gradle-not-including-dependencies-in-published-pom-xml
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}
